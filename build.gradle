plugins {
  id "com.github.spotbugs" version "6.1.10"
  id 'com.palantir.git-version' version '3.2.0'

  id 'java'
  id 'java-library'
  id 'maven-publish'
  id 'checkstyle'
  id 'signing'
  id 'org.owasp.dependencycheck' version '12.1.0'
}



group 'com.pippsford'
if ((versionDetails().branchName ==~ /^(main)|(master)|(release.*)|(patch.*)$/) && versionDetails().isCleanTag && gitVersion() ==~ /^\d+(\.\d+)+(-.+)?$/) {
  project.version = gitVersion()
} else {
  print versionDetails()
  project.version = '100-SNAPSHOT'
}



repositories {
  mavenCentral()
}



dependencies {
  implementation group: 'jakarta.json', name: 'jakarta.json-api', version: '2.1.3'
  implementation group: 'jakarta.annotation', name: 'jakarta.annotation-api', version: '3.0.0'
  implementation group: 'org.slf4j', name: 'slf4j-api', version: '2.0.17'
  implementation group: 'com.pippsford', name: 'common-utils', version: '1.15'
  implementation group: 'com.googlecode.owasp-java-html-sanitizer', name: 'owasp-java-html-sanitizer', version: '20260101.1'
  implementation group: 'com.github.spotbugs', name: 'spotbugs-annotations', version: '4.2.0'

  testImplementation group: 'com.pippsford', name: 'canonical-json', version: '3.3'
  testImplementation 'org.junit.jupiter:junit-jupiter-api:5.6.0'
  testImplementation group: 'com.google.jimfs', name: 'jimfs', version: '1.3.1'
  testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
}



checkstyle {
  toolVersion = "10.13.0"
  configFile = rootProject.file('config/checkstyle/checkstyle.xml')
}

checkstyleTest {
  enabled = false
}

// Guava conflict with checkstyle plugin.
configurations.checkstyle {
  resolutionStrategy.capabilitiesResolution.withCapability("com.google.collections:google-collections") {
    select("com.google.guava:guava:0")
  }
}



spotbugs {
  excludeFilter = rootProject.file('config/spotbugs/spotbugs-exclude.xml')
}

spotbugsMain {
  reports {
    html {
      required = true
    }
    xml {
      required = false
    }
  }
}

spotbugsTest {
  enabled = false
}

java {
  withSourcesJar()
  withJavadocJar()
  toolchain {
    languageVersion = JavaLanguageVersion.of(17)
  }
}

javadoc {
  options.addStringOption('Xmaxwarns', '65536')
}

test {
  useJUnitPlatform()
}

artifacts {
  archives javadocJar, sourcesJar
}

publishing {
  publications {
    mavenJava(MavenPublication) {
      from components.java
      pom {
        name = 'Stencil Templating System'
        packaging = 'jar'
        description = 'Stencil templating system'
        url = 'https://github.com/simon-greatrix/stencil'

        scm {
          connection = 'scm:git:https://github.com/simon-greatrix/stencil'
          developerConnection = 'scm:git:https://github.com/simon-greatrix/stencil'
          url = 'https://github.com/simon-greatrix/stencil'
        }

        licenses {
          license {
            name = 'The Apache License, Version 2.0'
            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
          }
        }

        developers {
          developer {
            id = 'simon-greatrix'
            name = 'Simon Greatrix'
            email = 'simon@pippsford.com'
          }
        }
      }
    }
  }

  repositories {
    maven {
      name = 'file'
      url = layout.buildDirectory.dir('repository')
    }
  }
}

tasks.withType(JavaCompile).configureEach {
  options.encoding = 'UTF-8'
}

tasks.withType(Javadoc).configureEach {
  options.encoding = 'UTF-8'
}

signing {
  useGpgCmd()
  sign publishing.publications.mavenJava
}

tasks.register("createCentralZip", Zip) {
  dependsOn tasks.named("publishMavenJavaPublicationToFileRepository")
  from layout.buildDirectory.dir("repository")
  destinationDirectory = layout.buildDirectory.dir("publications")
  archiveFileName = "central.zip"
}
